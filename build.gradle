import com.xpdustry.toxopid.spec.ModMetadata
import com.xpdustry.toxopid.spec.ModPlatform

plugins {
  id "java"
  id "com.xpdustry.toxopid" version "4.1.2"
  id "net.kyori.indra.publishing" version "3.1.3"
  id "com.gradleup.shadow" version "9.2.2"
}

def metadata = ModMetadata.fromJson(file("plugin.hjson"))

group = "com.xpdustry"
version = metadata.version
description = metadata.description
def _group = group.split("\\.").reverse()
def organizationId = _group[0..-2]*.toLowerCase().join("-")
def organizationName = _group[0..-2]*.capitalize().join(" ")
def organizationDomain = _group*.toLowerCase().join(".")

repositories {
  mavenCentral()
  maven { url "https://maven.${organizationDomain}/mindustry" }
  maven { url "https://www.jitpack.io" }
}

dependencies {
  compileOnly toxopid.dependencies.mindustryCore
  compileOnly toxopid.dependencies.arcCore
  implementation "org.fusesource.jansi:jansi-native:1.1"
  implementation "org.fusesource.jansi:jansi-native:1.1:windows32"
  implementation "org.fusesource.jansi:jansi-native:1.1:windows64"
}

// Force java 1.8
tasks.withType(JavaCompile) {
  targetCompatibility = JavaVersion.VERSION_1_8
  sourceCompatibility = JavaVersion.VERSION_1_8
  options.encoding = "UTF-8"
  options.compilerArgs.addAll(["--release", "8"])
}

toxopid {
  compileVersion = "v146" //"v${metadata.minGameVersion}"
  platforms = [ModPlatform.DESKTOP, ModPlatform.SERVER]
}

shadowJar {
  archiveFileName = "${metadata.name}.jar"

  // Include plugin json
  from(rootDir) {
    include "plugin.hjson"
  }

  // Copy the builded jar to the working directory
  doLast {
    copy {
      from shadowJar
      into rootDir
    }
  }

  // Make it smallest as possible
  minimize()
  exclude "META-INF/maven/**"
}

build.dependsOn shadowJar

// Publishing
signing {
  def signingKey = findProperty("signingKey")
  def signingPassword = findProperty("signingPassword")
  useInMemoryPgpKeys(signingKey, signingPassword)
}

indra {
  javaVersions {
    target(8)
    minimumToolchain(8)
  }
    
  mitLicense()
  publishReleasesTo(organizationId, "https://maven.${organizationDomain}/releases")

  def repo = metadata.repository.split("/")
  github(repo[0], repo[1]) {
    ci(true)
    issues(true)
    scm(true)
  }

  configurePublications {
    // Why isn't it already included?
    from components.java
    artifact sourcesJar
    artifact javadocJar
  
    pom {
      organization {
        name = organizationName
        url = "https://www.${organizationDomain}"
      }

      developers {
        metadata.author.split(",").each { a ->
          developer {
            id = a.trim()
            url = "https://github.com/${id.get()}"
          }
        }
      }
    }
  }
}

// Why don't these tasks already exist?
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')
    archiveClassifier = 'javadoc'
}
task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}
